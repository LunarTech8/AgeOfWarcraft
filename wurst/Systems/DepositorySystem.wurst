/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Handles storage buildings that act as ware drop off centres.
Workers fetch/deliver wares from/to other workplace buildings as required.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package DepositorySystem
import ConstantData
import UnitMap
import LinkedList
import Stock
import UtilityFunctions
import MultiboardPrioritySystem
import UnitIndexer
import OnUnitEnterLeave
import Icons
import RegisterEvents
import StorageSystem
import ErrorHandling


// --------------------
// Data code
// --------------------

let WORKERS_ICON_PATH = Icons.bTNBash

/** Returns the worker type of the given depository type. */
function getWorkerUnitId(int depositoryUnitId) returns int
	switch depositoryUnitId
		case UnitIdsCustom.AoW.townHall
			return UnitIdsCustom.AoW.peasant
	return 0

/** Returns the maximum ware capacity of the given depository type. */
function getDepositoryMaxCapacity(int depositoryUnitId) returns int
	switch depositoryUnitId
		case UnitIdsCustom.AoW.townHall
			return 500
		default
			return 0

/** Returns the maximum allowed amount of workers of the given depository type. */
public function getDepositoryMaxWorkers(int depositoryUnitId) returns int
	switch depositoryUnitId
		case UnitIdsCustom.AoW.townHall
			return 3
	return 0


// --------------------
// Functional code
// --------------------

let DEPOSITORY_MAP = new IterableUnitHashMap<Depository>(null)

public class Depository extends Storage
	private unit user
	private int gatherVariant
	private group workers

	construct(unit depository)
		super(depository, getDepositoryMaxCapacity(GetUnitTypeId(depository)))
		user = depository
		gatherVariant = 1
		workers = CreateGroup()

	ondestroy
		for worker in workers
			worker.show()
		workers.destr()

	function isValidWorkerType(int unitId) returns bool
		return getWorkerUnitId(user.getUnitId()) == unitId

	function getUnit() returns unit
		return user

	function getWorkerCount() returns int
		return workers.size()

	function getFirstWorker() returns unit
		if workers.size() > 0
			return workers.getUnitAt(0)
		else
			error("Depository doesn't have any worker")
			return null

	function isWorker(unit worker) returns bool
		return workers.contains(worker)

	function addWorker(unit worker)
		if workers.contains(worker) == false and workers.size() < getDepositoryMaxWorkers(user.getUnitId())
			workers.addUnit(worker)
		else
			error("Can't add worker to this depository")

	function removeWorker(unit worker)
		if workers.contains(worker)
			workers.removeUnit(worker)
		else
			error("Can't remove worker from this depository")

	function orderWorking()
		for worker in workers
			// TODO: only for workers that are in the building
			// TODO: find workshop that requires wares from depository and deliver them
			// TODO: else find fullest workshop/gatherplace and take wares from there
			// worker.issueTargetAbilityOrder(AbilityIdsCustom.transferWares, targetBuilding, true)

	/** Transfers as many wares as possible from source to this depository. */
	function transferWares(Storage source)
		// Look for wares in source storage:
		for iWare in source.wares
			let wareCount = min(iWare.count, getFreeCapacity())
			if wareCount > 0
				// Transfer ware:
				source.removeWare(ware(iWare.wareType, wareCount))
				addWare(ware(iWare.wareType, wareCount))


function unit.addDepository()
	if DEPOSITORY_MAP.has(this) == false and getDepositoryMaxWorkers(this.getUnitId()) > 0
		DEPOSITORY_MAP.put(this, new Depository(this))

function unit.removeDepository()
	let depository = DEPOSITORY_MAP.tryRemove(this)
	if depository != null
		destroy depository

/** Returns the depository of given unit. */
public function unit.getDepository() returns Depository
	return DEPOSITORY_MAP.tryGet(this)

/** Sets system relevant infos for given object in target multiboard. */
public function multiboard.setObjectInfoMultiboard(Depository d) returns bool
	this.addMultiboardLine("Depository:", HEADLINE_ICON_PATH)
	this.addMultiboardLine("Workers: " + d.getWorkerCount().toString(), WORKERS_ICON_PATH)
	this.addMultiboardLine("Capacity: " + d.getUsedCapacity().toString() + "/" + d.getMaxCapacity().toString(), SUB_HEADLINE_ICON_PATH)
	for ware in d.wares
		this.addMultiboardLine(d.getWareCount(ware.wareType).toString() + " " + ware.toString(true), ware.getIconPath())
	return true

init
	// Add depository to every appropriate new unit:
	onEnter() ->
		getEnterLeaveUnit().addDepository()
	// Remove depository from every appropriate removed unit:
	onUnitDeindex() ->
		getIndexingUnit().removeDepository()
	// Register construction finishes:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_FINISH) ->
		GetTriggerUnit().addDepository()
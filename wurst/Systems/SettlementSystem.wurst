/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Stores settlements and its buildings, persons and progress level.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package SettlementSystem
import UnitMap
import MultiboardPrioritySystem
import Icons
import UnitIndexer
import RegisterEvents
import Orders
import UtilityFunctions
import ConstantDataSystem
import UnitTest
import ClosureTimers
import StorageSystem
import Stock


// --------------------
// Data code
// --------------------

let LEVEL_ICON_PATH = Icons.bTNSnazzyScroll
let MAX_LEVEL = 3

function getBuildingRange(int level) returns real
	switch level
		case 1
			return 768.
		case 2
			return 1024.
		case 3
			return 1280.
	return 0.

function getLevel(int popCount) returns int
	if popCount >= 30
		return 3
	else if popCount >= 10
		return 2
	else if popCount >= 1
		return 1
	return 0

function getRequiredLevel(int buildingId) returns int
	switch buildingId
		case UnitIdsCustom.AoW.townHall
			return 0
		case UnitIdsCustom.AoW.keep
			return 0
		case UnitIdsCustom.AoW.castle
			return 0
		case UnitIdsCustom.AoW.peasantHouse
			return 1
		case UnitIdsCustom.AoW.lumberMill
			return 1
		case UnitIdsCustom.AoW.barracks
			return 1
		case UnitIdsCustom.AoW.blacksmith
			return 2
	return 1

function getInvalidMessageText(Validity val) returns string
	switch val
		case OUTSIDE_RANGE
			return "Can't be constructed outside the range of a settlement"
		case INVALID_LEVEL
			return "Settlement level to low for this building type"
		case TOO_CLOSE
			return "Can't be founded that close to another settlement"
		case ALREADY_ADDED
			return "Is already a settlement"
		case INVALID_TYPE
			return "Invalid building type to found a settlement"
		default
			Log.error("ERROR: Unregistered invalidity message text for " + (val castTo int).toString())
			return null


// --------------------
// Functional code
// --------------------

let SETTLEMENT_MAP = new IterableUnitHashMap<Settlement>(null)

enum Validity
	VALID
	OUTSIDE_RANGE
	INVALID_LEVEL
	TOO_CLOSE
	ALREADY_ADDED
	INVALID_TYPE

class Settlement
	private unit centreBuilding
	private group buildings
	private group persons
	protected int level

	construct(unit centreBuilding)
		this.centreBuilding = centreBuilding
		buildings = CreateGroup()
		persons = CreateGroup()
		updateLevel()

	ondestroy
		for building in buildings
			building.setOwner(Player(PLAYER_NEUTRAL_PASSIVE), true)
		buildings.destr()
		persons.destr()

	private function updateLevel()
		level = getLevel(persons.size())

	function isMember(unit member) returns bool
		if member.isType(UNIT_TYPE_STRUCTURE)
			return buildings.contains(member)
		else
			return persons.contains(member)

	function addMemeber(unit member)
		if member.isType(UNIT_TYPE_STRUCTURE)
			buildings.addUnit(member)
		else
			persons.addUnit(member)
			updateLevel()

	function removeMember(unit member)
		if member.isType(UNIT_TYPE_STRUCTURE)
			buildings.removeUnit(member)
		else
			persons.removeUnit(member)
			updateLevel()


function isCentreBuilding(int buildingId) returns bool
	return getRequiredLevel(buildingId) == 0

function unit.addSettlement() returns Validity
	// Check validity:
	if SETTLEMENT_MAP.has(this)
		return Validity.ALREADY_ADDED
	if isCentreBuilding(this.getUnitId()) == false
		return Validity.INVALID_TYPE
	let pos = this.getPos()
	let range = 2 * getBuildingRange(MAX_LEVEL)
	for centreBuilding in SETTLEMENT_MAP
		if pos.distanceTo(centreBuilding.getPos()) <= range
			return Validity.TOO_CLOSE
	// Add settlement and nearby units:
	let settlement = new Settlement(this)
	SETTLEMENT_MAP.put(this, settlement)
	ENUM_GROUP.enumUnitsInRange(this.getPos(), getBuildingRange(settlement.level), null)
	let owner = this.getOwner()
	for targetUnit from ENUM_GROUP
		if targetUnit.getOwner() == owner and targetUnit.getSettlement() == null
			settlement.addMemeber(targetUnit)
	return Validity.VALID

function unit.addToSettlementInRange() returns Validity
	let pos = this.getPos()
	for centreBuilding in SETTLEMENT_MAP
		let settlement = SETTLEMENT_MAP.get(centreBuilding)
		if pos.distanceTo(centreBuilding.getPos()) <= getBuildingRange(settlement.level)
			if getRequiredLevel(this.getUnitId()) > settlement.level
				return Validity.INVALID_LEVEL
			settlement.addMemeber(this)
			return Validity.VALID
	return Validity.OUTSIDE_RANGE

function unit.removeFromSettlement()
	var settlement = SETTLEMENT_MAP.tryRemove(this)
	if settlement != null
		destroy settlement  // Destroy settlement if centre building gets removed
	else
		settlement = this.getSettlement()
		if settlement != null
			settlement.removeMember(this)

/** Returns the settlement of given unit. */
public function unit.getSettlement() returns Settlement
	var settlement = SETTLEMENT_MAP.tryGet(this)  // Check if it is the centre building
	if settlement == null
		for centreBuilding in SETTLEMENT_MAP
			settlement = SETTLEMENT_MAP.get(centreBuilding)
			if settlement.isMember(this)
				return settlement
		settlement = null
	return settlement

/** Trys to add this unit as settlement. Returns if it could be added. */
public function unit.tryAddSettlement() returns bool
	return this.addSettlement() == Validity.VALID

/** Sets system relevant infos for given object in target multiboard. */
public function multiboard.setObjectInfoMultiboard(Settlement s) returns bool
	this.addMultiboardLine("Settlement:", HEADLINE_ICON_PATH)
	this.addMultiboardLine("Level: " + s.level.toString(), LEVEL_ICON_PATH)
	return true

init
	// Remove settlement from every removed centre building:
	onUnitDeindex() ->
		getIndexingUnit().removeFromSettlement()
	// Register upgrade starts:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_START) ->
		let building = GetTriggerUnit()
		Validity val
		if isCentreBuilding(building.getUnitId())
			val = building.addSettlement()
		else
			val = building.addToSettlementInRange()
		if val != Validity.VALID
			printTimedToPlayer("Invalid: " + getInvalidMessageText(val), INFO_MSG_TIME, building.getOwner())
			building.issueImmediateOrderById(SpecialOrders.cancel)


// --------------------
// Test code
// --------------------

unit testSettlementSystem_Build_building = null
public function testSettlementSystem_Build(vec2 testCentre)
	let testName = setTestName("testSettlementSystem_Build", true)
	// Remove units blocking the building:
	ForGroup(GetUnitsInRectAll(testCentre.withRadiusRect(128.)), () -> GetEnumUnit().remove())
	// Order to build:
	let unitA = createUnit(Player(0), UnitIdsCustom.AoW.peasant, testCentre, angle(0))
	let storageA = unitA.getStorage()
	assertNotNull(testName, storageA)
	storageA.addWare(ware(WareType.WOOD, 4))
	storageA.addWare(ware(WareType.STONE, 3))
	storageA.addWare(ware(WareType.METAL, 2))
	unitA.issuePointOrderById(UnitIdsCustom.AoW.townHall, testCentre)
	testSettlementSystem_Build_building = null
	doAfter(MIN_ACTION_SLEEP_TIME) () ->
		// Check unit mappings:
		let a = unitA.getBuildingSiteOfWorker()  // FIXME: can't import BuildingSystem because of cyclic dependancy
		assertNotNull(testName, a)
		testSettlementSystem_Build_building = a.getUnit()
		assertNotNull(testName, testSettlementSystem_Build_building)
		assertEquals(testName, testSettlementSystem_Build_building.getBuildingSite(), a)
		let settlementA = testSettlementSystem_Build_building.getSettlement()
		assertNotNull(testName, settlementA)
		// TODO: implement
	doAfter(2 * MIN_ACTION_SLEEP_TIME) () ->
		// Abort construction:
		testSettlementSystem_Build_building.issueImmediateOrderById(SpecialOrders.cancel)
	doAfter(3 * MIN_ACTION_SLEEP_TIME) () ->
		// Remove test units:
		unitA.kill()
		unitA.remove()
/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Contains generic utility functions that aren't tied directly to a specific system.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package UtilityFunctions
import LinkedList
import HashMap
import ConstantData
import Orders
import ObjectIds
import ErrorHandling
import Destructable_Spruce
import Destructable_Maple
import DestructablePreset


// --------------------
// Data code
// --------------------

// Time:
public let INFO_MSG_TIME = 2.
public let MIN_TRIGGER_SLEEP_TIME = 0.01
public let MIN_ACTION_SLEEP_TIME = 0.25
public let WAIT_SLEEP_TIME = 0.25
public let INGAME_DAY_DURATION = 480.  // In realtime seconds
// Terrain:
public let HEIGHT_DEEP_WATER_MIN = -64.
public let HEIGHT_SHALLOW_WATER_MIN = 112.
public let HEIGHT_LAND_MIN = 168.
public let HEIGHT_LAND_MAX = 2048.
public let TILE_DISTANCE = 128.

function initBaseUnitTypeIdMap() returns HashMap<int, int>
	let map = new HashMap<int, int>
	// map.put(UnitIdsCustom.Trees.beech_Var1, UnitIdsCustom.Trees.beech)
	// map.put(UnitIdsCustom.Trees.beech_Var2, UnitIdsCustom.Trees.beech)
	// map.put(UnitIdsCustom.Trees.beech_Var3, UnitIdsCustom.Trees.beech)
	// map.put(UnitIdsCustom.Trees.beech_Var4, UnitIdsCustom.Trees.beech)
	// map.put(UnitIdsCustom.Trees.birch_Var1, UnitIdsCustom.Trees.birch)
	// map.put(UnitIdsCustom.Trees.birch_Var2, UnitIdsCustom.Trees.birch)
	// map.put(UnitIdsCustom.Trees.birch_Var3, UnitIdsCustom.Trees.birch)
	// map.put(UnitIdsCustom.Trees.greyOak_Var1, UnitIdsCustom.Trees.greyOak)
	// map.put(UnitIdsCustom.Trees.greyOak_Var2, UnitIdsCustom.Trees.greyOak)
	// map.put(UnitIdsCustom.Trees.greyOak_Var3, UnitIdsCustom.Trees.greyOak)
	// map.put(UnitIdsCustom.Trees.greyOak_Var4, UnitIdsCustom.Trees.greyOak)
	// map.put(UnitIdsCustom.Trees.chestnut_Var1, UnitIdsCustom.Trees.chestnut)
	// map.put(UnitIdsCustom.Trees.chestnut_Var2, UnitIdsCustom.Trees.chestnut)
	return map

/** Returns the unit type id of a random variation of this base unit id. Required for unit creation. */
public function int.getUnitTypeId() returns int
	// switch this
	// 	case UnitIdsCustom.Trees.beech
	// 		return getRandomInputInt(UnitIdsCustom.Trees.beech_Var1, UnitIdsCustom.Trees.beech_Var2, UnitIdsCustom.Trees.beech_Var3, UnitIdsCustom.Trees.beech_Var4)
	// 	case UnitIdsCustom.Trees.birch
	// 		return getRandomInputInt(UnitIdsCustom.Trees.birch_Var1, UnitIdsCustom.Trees.birch_Var2, UnitIdsCustom.Trees.birch_Var3)
	// 	case UnitIdsCustom.Trees.greyOak
	// 		return getRandomInputInt(UnitIdsCustom.Trees.greyOak_Var1, UnitIdsCustom.Trees.greyOak_Var2, UnitIdsCustom.Trees.greyOak_Var3, UnitIdsCustom.Trees.greyOak_Var4)
	// 	case UnitIdsCustom.Trees.chestnut
	// 		return getRandomInputInt(UnitIdsCustom.Trees.chestnut_Var1, UnitIdsCustom.Trees.chestnut_Var2)
	return this

/** Returns the replacing destructable type id of the given destructable type. */
public function getReplacingDestructableTypeId(int destructableTypeId) returns int
	switch destructableTypeId
		case DestructableIds.summerTreeWall
			return DestructableIdsCustom.Trees.spruce
		case DestructableIds.felwoodCanopyTree
			return DestructableIdsCustom.Trees.maple
	return 0

/** Returns a random variation for of given destructable type. */
public function getRandomDestructableVariation(int destructableTypeId) returns int
	switch destructableTypeId
		case DestructableIdsCustom.Trees.spruce
			return getRandomVariation(DestructableData_Spruce.variations)
		case DestructableIdsCustom.Trees.maple
			return getRandomVariation(DestructableData_Maple.variations)
	return 0

/** Returns a random scaling for of given destructable type. */
public function getRandomDestructableScale(int destructableTypeId) returns real
	switch destructableTypeId
		case DestructableIdsCustom.Trees.spruce
			return GetRandomReal(DestructableData_Spruce.minScale, DestructableData_Spruce.maxScale)
		case DestructableIdsCustom.Trees.maple
			return GetRandomReal(DestructableData_Maple.minScale, DestructableData_Maple.maxScale)
	return 1.

/** Returns the order needed to cast this ability id. */
public function int.getAbilityOrder() returns string
	switch this
		case AbilityIdsCustom.harvest
			return "thunderbolt"
		case AbilityIdsCustom.build
			return "shadowstrike"
		case AbilityIdsCustom.showTargetInfo
			return "thunderbolt"
		case AbilityIdsCustom.transferWares
			return "frostnova"
		case AbilityIdsCustom.addBuildingBuff
			return "innerfire"
		case AbilityIdsCustom.enterBuilding
			return "innerfire"
		case AbilityIdsCustom.targetDestructable
			return "harvest"
		case AbilityIdsCustom.addSleepingBuff
			return "sleep"
		case AbilityIdsCustom.addExhaustedBuff
			return "cripple"
		case AbilityIdsCustom.changeProduct
			return "fanofknives"
		case AbilityIdsCustom.removeWorker
			return "thunderclap"
		default
			error("Unmapped ability id (" + this.toString() + ")")
			return null

/** Returns the ability id used for this order name. */
public function string.getOrderNameAbility() returns int
	switch this
		case "harvest"
			return AbilityIdsCustom.harvest
		case "build"
			return AbilityIdsCustom.build
		case "show target info"
			return AbilityIdsCustom.showTargetInfo
		case "transfer wares"
			return AbilityIdsCustom.transferWares
		case "add building buff"
			return AbilityIdsCustom.addBuildingBuff
		case "enter building"
			return AbilityIdsCustom.enterBuilding
		case "target destructable"
			return AbilityIdsCustom.targetDestructable
		default
			error("Unmapped order name (" + this + ")")
			return 0

/** Returns the order name of this order or ability id. */
public function int.getOrderName() returns string
	switch this
		case OrderIds.smart
			return "smart"
		case OrderIds.move
			return "move"
		case OrderIds.stop
			return "stop"
		case OrderIds.attack
			return "attack"
		case OrderIds.attackground
			return "attack ground"
		case OrderIds.holdposition
			return "hold position"
		case OrderIds.patrol
			return "patrol"
		case OrderIds.repair
			return "repair"
		case OrderIds.repairon
			return "repair on"
		case OrderIds.repairoff
			return "repair off"
		case OrderIds.stunned
			return "stunned"
		case OrderIds.undefend
			return null
		case AbilityIdsCustom.harvest
			return "harvest"
		case AbilityIdsCustom.showTargetInfo
			return "show target info"
		case AbilityIdsCustom.build
			return "build"
		case AbilityIdsCustom.transferWares
			return "transfer wares"
		case AbilityIdsCustom.addBuildingBuff
			return "add building buff"
		case AbilityIdsCustom.enterBuilding
			return "enter building"
		case AbilityIdsCustom.targetDestructable
			return "target destructable"
		default
			if this < 2013265920  // Ignore unit ids
				error("Unmapped order/ability id (" + this.toString() + ")")
			return null
/** Returns the order name of this order id of the given unit. Returns null if onlyCustomAbilities is given and order doesn't fit to a custom ability of the given unit. */
public function int.getOrderName(unit orderedUnit, bool onlyCustomAbilities) returns string
	switch this
		case OrderIds.thunderbolt
			if orderedUnit.hasAbility(AbilityIdsCustom.harvest)
				return "harvest"
			else if orderedUnit.hasAbility(AbilityIdsCustom.showTargetInfo)
				return "show target info"
		case OrderIds.shadowstrike
			if orderedUnit.hasAbility(AbilityIdsCustom.build)
				return "build"
		case OrderIds.frostnova
			if orderedUnit.hasAbility(AbilityIdsCustom.transferWares)
				return "transfer wares"
		case OrderIds.innerfire
			if orderedUnit.hasAbility(AbilityIdsCustom.addBuildingBuff)
				return "add building buff"
			else if orderedUnit.hasAbility(AbilityIdsCustom.enterBuilding)
				return "enter building"
		case OrderIds.harvest
			if orderedUnit.hasAbility(AbilityIdsCustom.targetDestructable)
				return "target destructable"
		default
			if onlyCustomAbilities
				return null
			else
				return this.getOrderName()
	error("Unmapped order id (" + this.toString() + ")")
	return null
/** Returns the order name of this order id of the given unit. */
public function int.getOrderName(unit orderedUnit) returns string
	return this.getOrderName(orderedUnit, false)

// --------------------
// Functional code
// --------------------

let BASE_UNIT_TYPE_ID_MAP = initBaseUnitTypeIdMap()

/** Returns the base unit id of this unit. Required for data evaluation. */
public function unit.getUnitId() returns int
	let id = GetUnitTypeId(this)
	if BASE_UNIT_TYPE_ID_MAP.has(id)
		return BASE_UNIT_TYPE_ID_MAP.get(id)
	return id

/** Returns the nearest unit from this vector in given group of given player. Clear given group if required. */
public function vec2.getNearestUnit(group validUnits, bool clearGroup, player owner) returns unit
	// Set init values:
	var bestDistanceSq = REAL_MAX
	unit nearestUnit = null
	// Find nearest unit:
	if clearGroup
		for target from validUnits
			if owner == null or target.getOwner() == owner
				let distanceSq = this.distanceToSq(target.getPos())
				if distanceSq < bestDistanceSq
					bestDistanceSq = distanceSq
					nearestUnit = target
	else
		for target in validUnits
			if owner == null or target.getOwner() == owner
				let distanceSq = this.distanceToSq(target.getPos())
				if distanceSq < bestDistanceSq
					bestDistanceSq = distanceSq
					nearestUnit = target
	return nearestUnit
/** Returns the nearest unit from this vector in given group. Clear given group if required. */
public function vec2.getNearestUnit(group validUnits, bool clearGroup) returns unit
	return this.getNearestUnit(validUnits, clearGroup, null)
/** Returns the nearest unit from this vector in given max range of given player. */
public function vec2.getNearestUnit(real maxRange, boolexpr filter, player owner) returns unit
	// Filter out invalid units in range:
	ENUM_GROUP.enumUnitsInRange(this, maxRange, filter)
	// Find nearest unit:
	return this.getNearestUnit(ENUM_GROUP, true, owner)
/** Returns the nearest unit from this vector in given max range. */
public function vec2.getNearestUnit(real maxRange, boolexpr filter) returns unit
	return this.getNearestUnit(maxRange, filter, null)

/** Returns the nearest destructable from this vector in given list.*/
public function vec2.getNearestDestructable(LinkedList<destructable> validDestructables) returns destructable
	// Set init values:
	var bestDistanceSq = REAL_MAX
	destructable nearestDest = null
	// Find nearest destructable:
	for target in validDestructables
		let distanceSq = this.distanceToSq(target.getPos())
		if distanceSq < bestDistanceSq
			bestDistanceSq = distanceSq
			nearestDest = target
	return nearestDest

/** Issues this unit to cast given ability on target widget. The ability can be reset beforehand to bypass the cooldown. */
public function unit.issueTargetAbilityOrder(int abilityId, widget target, bool resetAbility) returns bool
	if resetAbility
		this.removeAbility(abilityId)
		this.addAbility(abilityId)
	return this.issueTargetOrder(abilityId.getAbilityOrder(), target)

/** Replaces this unit with a new unit of given type. */
public function unit.replace(int newUnitTypeId) returns unit
	this.hide()
	let newUnit = createUnit(this.getOwner(), newUnitTypeId, this.getPos(), this.getFacingAngle())
	this.kill()
	this.remove()
	return newUnit

/** Replaces this destructable with a new destructable of given type. */
public function destructable.replace(int newDestructableTypeId) returns destructable
	this.show(false)
	let newDestructable = createDestructable(newDestructableTypeId, this.getPos(), getRandomAngle(), getRandomDestructableScale(newDestructableTypeId), getRandomDestructableVariation(newDestructableTypeId))
	this.kill()
	this.remove()
	return newDestructable

/** Returns a string of the given unit ids. */
public function getUnitIdsString(vararg int unitIds) returns string
	var unitString = ""
	for unitId in unitIds
		unitString += unitId.toRawCode() + ","
	if unitString != ""
		unitString = unitString.substring(0, unitString.length()-1)
	return unitString

/** Returns random value of given inputs. */
public function getRandomInputInt(vararg int inputs) returns int
	var count = 0
	for _input in inputs
		count += 1
	let i = GetRandomInt(1, count)
	count = 0
	for input in inputs
		count += 1
		if i == count
			return input
	return 0

/** Returns random value of given inputs. */
public function getRandomInputReal(vararg real inputs) returns real
	var count = 0
	for _input in inputs
		count += 1
	let i = GetRandomInt(1, count)
	count = 0
	for input in inputs
		count += 1
		if i == count
			return input
	return 0

/** Returns random angle (bewteen 0 and 2*pi). */
public function getRandomAngle() returns angle
	return angle(GetRandomReal(0., PI2))
/** Returns random angle bewteen min and max. */
public function getRandomAngle(real min, real max) returns angle
	if min < 0. or max > PI2 or min > max
		error("Invalid min and/or max value")
	return angle(GetRandomReal(min, max))

/** Returns random sign (-1 or 1). */
public function getRandomSign() returns int
	return (GetRandomInt(0, 1) == 0) ? -1 : 1

/** Returns random value between 0 and given maxVariations. */
public function getRandomVariation(int maxVariations) returns int
	return GetRandomInt(0, maxVariations - 1)

/** Throws an exception if the absolute of this value is smaller than epsilon, else returns the value. */
public function real.divByZeroProt(real epsilon) returns real
	if this.abs() <= epsilon
		error("Invalid divisor value (" + this.toString() + ")")
	return this
/** Throws an exception if the absolute of the given value is smaller than 10^-7, else returns the value. */
public function real.divByZeroProt() returns real
	return this.divByZeroProt(0.0000001)
/** Throws an exception if the given value is 0, else returns the value. */
public function int.divByZeroProt() returns int
	if this == 0
		error("Invalid divisor value (" + this.toString() + ")")
	return this

/** Shows given image covering the full screen. Activates cinematic mode if use is given. */
public function showFullscreenImage(string imagePath, bool useCinematicMode)
	if useCinematicMode
		// CinematicModeBJ(true, GetPlayersAll())  // DEBUG: only for developement
	SetCineFilterTexture(imagePath)
	SetCineFilterBlendMode(BLEND_MODE_NONE)
	SetCineFilterTexMapFlags(TEXMAP_FLAG_NONE)
	SetCineFilterStartUV(0, 0, 1, 1)
	SetCineFilterEndUV(0, 0, 1, 1)
	SetCineFilterStartColor(255, 255, 255, 255)
	SetCineFilterEndColor(255, 255, 255, 255)
	SetCineFilterDuration(0.)
	DisplayCineFilter(true)

/** Hides image covering the full screen. Deactivates cinematic mode if use is given. */
public function hideFullscreenImage(bool useCinematicMode)
	DisplayCineFilter(false)
	if useCinematicMode
		CinematicModeBJ(false, GetPlayersAll())
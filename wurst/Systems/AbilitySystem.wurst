/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Handles all custom abilities and allows for the addition of custom, unrestricted functionality.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package AbilitySystem
import RegisterEvents
import ConstantData
import UtilityFunctions
import Entity
import Stock
import StorageSystem
import BuildingSystem
import ResourceSystem
import OrderSystem
import Ability_Harvest
import InstantDummyCaster
import DummyRecycler
import ClosureTimers
import WorkshopSystem
import HomeSystem
import GatherplaceSystem
import DepositorySystem
import PersonSystem
import SettlementSystem
import ErrorHandling
import WorkplaceSystem
import LinkedList


// --------------------
// Data code
// --------------------

let MAX_AUTO_ORDER_RANGE = 1024.

function getInvalidMessageText(int abilityId, Validity val) returns string
	switch abilityId
		case AbilityIdsCustom.transferWares
			switch val
				case INVALID_SOURCE
					return "Unit can't transfer wares"
				case INVALID_TARGET
					return "Can't transfer wares to this kind of target"
				case WARES_NOT_AVAILABLE
					return "No wares to transfer are available"
				case NO_FREE_STORAGE
					return "Target has no free storage for transfer"
				case NO_REQUIRED_WARES
					return "Target has no required wares left"
				default
					return null
		case AbilityIdsCustom.build
			switch val
				case INVALID_SOURCE
					return "Worker can't build at target building site type"
				case INVALID_TARGET
					return "Build target is no building site"
				case WRONG_SETTLEMENT
					return "Building site belongs to another settlement"
				case WARES_NOT_AVAILABLE
					return "Not all required wares are available for building"
				case WORKER_ALREADY_BUILDING
					return "Worker is already building"
				case NO_FREE_UNIT_SLOT
					return "Building site already reached its maximum of concurrent workers"
				default
					return null
		case AbilityIdsCustom.harvest
			switch val
				case INVALID_SOURCE
					return "No storage for harvest"
				case INVALID_TARGET
					return "Harvest target is no resource"
				case NO_FREE_UNIT_SLOT
					return "Target already reached its maximum of concurrent harvesters"
				case NO_FREE_STORAGE
					return "No free storage for harvest"
				case RESOURCE_IS_DEPLETED
					return "Harvest target is already depleted"
				default
					return null
		case AbilityIdsCustom.enterBuilding
			switch val
				case INVALID_SOURCE
					return "Unit can't enter target building type"
				case INVALID_TARGET
					return "Can't enter this kind of building"
				case WRONG_SETTLEMENT
					return "Can't enter building of another settlement"
				case NO_FREE_UNIT_SLOT
					return "Target already reached its maximum of concurrent units"
				default
					return null
		case AbilityIdsCustom.removeWorker
			switch val
				case WORKERS_NOT_AVAILABLE
					return "No workers left to remove"
				default
					return null
		case AbilityIdsCustom.targetDestructable
			switch val
				case INVALID_TARGET
					return "Target is no destructable"
				default
					return null
	return null


// --------------------
// Functional code
// --------------------

enum Validity
	VALID
	UNREGISTERED
	ERROR
	INVALID_SOURCE
	INVALID_TARGET
	WRONG_SETTLEMENT
	WARES_NOT_AVAILABLE
	WORKERS_NOT_AVAILABLE
	NO_FREE_UNIT_SLOT
	NO_FREE_STORAGE
	NO_REQUIRED_WARES
	WORKER_ALREADY_BUILDING
	RESOURCE_IS_DEPLETED

/** Perform harvest and returns if another harvest with the given target is still valid. */
function unit.harvest(entity target) returns bool
	let storage = this.getStorage()
	let resource = target.getResource(true)
	// Work resource:
	let wares = resource.harvest(this.getUnitId())
	if wares != null
		// Transfere wares:
		for iWare in wares
			// Check resource count:
			let resourceWareCount = resource.getWareCount(iWare.wareType)
			let freeCapacity = storage.getFreeCapacity()
			if resourceWareCount > 0 and freeCapacity > 0
				// Limit by resource count:
				if resourceWareCount < iWare.count
					iWare.count = resourceWareCount
				// Limit by storage free capacity:
				if freeCapacity < iWare.count
					iWare.count = freeCapacity
				// Add ware to storage and remove from resource:
				storage.addWare(iWare)
				resource.removeWare(iWare)
		destroy wares
		// Check for resource depletion:
		if resource.checkForDepletionKill()
			target.kill()
			return false
	return this.isAbilityValid(AbilityIdsCustom.harvest, target)

/** Transfer wares between this unit and the given target. */
function unit.transferWares(unit target)
	// Check if wares can be transfered:
	let sourceStorage = this.getStorage()
	if sourceStorage != null
		// Check for possible transfer methods based on target:
		let targetStorage = target.getStorage()
		let targetWorkplace = target.getWorkplace()
		if targetStorage != null
			// Give wares to target storage:
			sourceStorage.transferWares(targetStorage)
		else if targetWorkplace == null
			error("No workplace type")
		else if targetWorkplace instanceof BuildingSite
			let targetBuildingSite = (targetWorkplace castTo BuildingSite)
			// Take wares from source storage:
			targetBuildingSite.transferWares(sourceStorage, true)
			// Call over idel workers if no required wares are missing:
			if targetBuildingSite.getStock().isEmpty()
				targetBuildingSite.callOverIdleWorkers()
		else if targetWorkplace instanceof Workshop
			// Exchange wares with source storage:
			(targetWorkplace castTo Workshop).transferWares(sourceStorage, true)
		else if targetWorkplace instanceof Gatherplace
			let targetGatherplace = (targetWorkplace castTo Gatherplace)
			if targetGatherplace.isWorker(this)
				// Give wares to target storage:
				targetGatherplace.transferWares(sourceStorage, false)
			else
				// Take wares from target storage:
				targetGatherplace.transferWares(sourceStorage, true)
		else if targetWorkplace instanceof Depository
			// Give wares to target storage:
			(targetWorkplace castTo Depository).transferWares(sourceStorage, false)

/** Returns the next allowed resource at given position for this person unit. */
function unit.findNextAllowedResource(real maxRange, vec2 pos) returns entity
	let sourceWorkplaceBuilding = this.getPerson().getWorkplaceBuilding()
	let sourceGatherplace = (sourceWorkplaceBuilding != null ? sourceWorkplaceBuilding.getGatherplace() : null)
	let allowedTypeIds = (sourceGatherplace != null ? sourceGatherplace.getAllowedTargetTypeIds() : new LinkedList<int>())
	let nextResource = this.findNextResource(maxRange, allowedTypeIds, pos)
	destroy allowedTypeIds
	return nextResource
/** Returns the next allowed resource for this person unit. */
function unit.findNextAllowedResource(real maxRange) returns entity
	return this.findNextAllowedResource(maxRange, this.getPos())

/** Run general reaction on invalid ability command. */
function unit.reactOnInvalidAbility(int abilityId, Validity val)
	if val == Validity.VALID or val == Validity.UNREGISTERED
		error("Given validity is not invalid (" + (val castTo int).toString() + ")")
	// React on invalid command:
	let owner = this.getOwner()
	if IsUnitSelected(this, owner)
		let messageText = getInvalidMessageText(abilityId, val)
		if messageText == null
			error("Unregistered invalidity message text for " + abilityId.toString() + " / " + (val castTo int).toString())
		printTimedToPlayer("Invalid: " + messageText, INFO_MSG_TIME, owner)
	this.abortOrder()

/** Returns the units validity for casting given ability on given target. */
function unit.getAbilityValidity(int abilityId, entity target) returns Validity
	if abilityId == AbilityIdsCustom.transferWares
		if target.userUnit == null
			return Validity.INVALID_TARGET
		let targetUnit = target.assertUnit()
		// Check if source is valid:
		let sourceStorage = this.getStorage()
		if sourceStorage == null
			return Validity.INVALID_SOURCE
		// Check target type specific validities:
		let targetStorage = targetUnit.getStorage()
		let targetWorkplace = targetUnit.getWorkplace()
		if targetStorage != null
			// Check if source has wares that can be transfered:
			if sourceStorage.getUsedCapacity() <= 0
				return Validity.WARES_NOT_AVAILABLE
			// Check if target has free storage:
			if targetStorage.getFreeCapacity() <= 0
				return Validity.NO_FREE_STORAGE
		else if targetWorkplace == null
			error("No workplace type")
		else if targetWorkplace instanceof BuildingSite
			let targetBuildingSite = (targetWorkplace castTo BuildingSite)
			// Check if target requires wares:
			if targetBuildingSite.getStock().isEmpty()
				return Validity.NO_REQUIRED_WARES
			// Check if source has resources that can be transfered:
			if sourceStorage.hasWaresAny(targetBuildingSite.getStock()) == false
				return Validity.WARES_NOT_AVAILABLE
		else if targetWorkplace instanceof Workshop
			let targetWorkshop = (targetWorkplace castTo Workshop)
			// Check if source has educts or target has products that can be transfered:
			let educts = targetWorkshop.getEducts()
			let hasAnyEducts = sourceStorage.hasWaresAny(educts)
			destroy educts
			if hasAnyEducts == false and targetWorkshop.getStoredProductsCount() <= 0
				return Validity.WARES_NOT_AVAILABLE
		else if targetWorkplace instanceof Gatherplace
			let targetGatherplace = (targetWorkplace castTo Gatherplace)
			// Check if source has products or target has wares that can be transfered:
			let products = targetGatherplace.getProducts()
			let hasAnyProducts = sourceStorage.hasWaresAny(products)
			destroy products
			if hasAnyProducts == false and targetGatherplace.getStorage().getUsedCapacity() <= 0
				return Validity.WARES_NOT_AVAILABLE
			// Check if target has free storage for products:
			if hasAnyProducts and targetGatherplace.getStorage().getFreeCapacity() <= 0
				return Validity.NO_FREE_STORAGE
		else if targetWorkplace instanceof Depository
			// Check if source has wares that can be transfered:
			if sourceStorage.getUsedCapacity() <= 0
				return Validity.WARES_NOT_AVAILABLE
			// Check if target has free storage:
			if (targetWorkplace castTo Depository).getStorage().getFreeCapacity() <= 0
				return Validity.NO_FREE_STORAGE
		else
			return Validity.INVALID_TARGET
	else if abilityId == AbilityIdsCustom.build
		if target.userUnit == null
			return Validity.INVALID_TARGET
		let targetUnit = target.assertUnit()
		// Check if source and target are valid:
		if targetUnit.getUnitId().isValidWorkerType(this.getUnitId()) == false
			return Validity.INVALID_SOURCE
		let buildingSite = targetUnit.getBuildingSite()
		if buildingSite == null
			return Validity.INVALID_TARGET
		// Check if source belongs to settlement of target:
		if this.getSettlement() != targetUnit.getSettlement()
			return Validity.WRONG_SETTLEMENT
		// Check if required wares are available:
		if buildingSite.getStock().isEmpty() == false
			return Validity.WARES_NOT_AVAILABLE
		// Check if worker is already building:
		if this.getBuildingSiteOfWorker() != null
			return Validity.WORKER_ALREADY_BUILDING
		// Check if target has free worker slot:
		if buildingSite.getFreeWorkerSlotsForBuildingSite() <= 0
			return Validity.NO_FREE_UNIT_SLOT
	else if abilityId == AbilityIdsCustom.harvest
		// Check if source and target are valid:
		let storage = this.getStorage()
		if storage == null
			return Validity.INVALID_SOURCE
		let resource = target.getResource(false)
		if resource == null
			return Validity.INVALID_TARGET
		// Check if wares can be harvested:
		if storage.getFreeCapacity() <= 0
			return Validity.NO_FREE_STORAGE
		else if resource.isEmpty()
			return Validity.RESOURCE_IS_DEPLETED
		else if resource.getFreeWorkerSlots() <= 0
			return Validity.NO_FREE_UNIT_SLOT
	else if abilityId == AbilityIdsCustom.enterBuilding
		if target.userUnit == null
			return Validity.INVALID_TARGET
		let targetUnit = target.assertUnit()
		// Check if source belongs to settlement of target:
		if this.getSettlement() != targetUnit.getSettlement()
			return Validity.WRONG_SETTLEMENT
		// Check target type specific validities:
		let targetHome = targetUnit.getHome()
		let targetWorkplace = targetUnit.getWorkplace()
		if targetHome != null
			// Check if source can inhabit target:
			if targetHome.isValidInhabitantType(this.getUnitId()) == false
				return Validity.INVALID_SOURCE
			// Check if source is inhabitant or target has free apartement slot:
			if targetHome.isInhabitant(this) == false and targetHome.getFreeApartementSlots() <= 0
				return Validity.NO_FREE_UNIT_SLOT
		else if targetWorkplace == null
			error("No workplace type")
		else if targetWorkplace instanceof Workshop
			let targetWorkshop = (targetWorkplace castTo Workshop)
			// Check if source is allowed to work at target:
			if targetWorkshop.isValidWorkerType(this.getUnitId()) == false
				return Validity.INVALID_SOURCE
			// Check if target works at target or has free worker slot:
			if targetWorkshop.isWorker(this) == false and targetWorkshop.getFreeWorkerSlots() <= 0
				return Validity.NO_FREE_UNIT_SLOT
		else if targetWorkplace instanceof Gatherplace
			let targetGatherplace = (targetWorkplace castTo Gatherplace)
			// Check if source is allowed to work at target:
			if targetGatherplace.isValidWorkerType(this.getUnitId()) == false
				return Validity.INVALID_SOURCE
			// Check if target works at target or has free worker slot:
			if targetGatherplace.isWorker(this) == false and targetGatherplace.getFreeWorkerSlots() <= 0
				return Validity.NO_FREE_UNIT_SLOT
		else if targetWorkplace instanceof Depository
			let targetDepository = (targetWorkplace castTo Depository)
			// Check if source is allowed to work at target:
			if targetDepository.isValidWorkerType(this.getUnitId()) == false
				return Validity.INVALID_SOURCE
			// Check if target works at target or has free worker slot:
			if targetDepository.isWorker(this) == false and targetDepository.getFreeWorkerSlots() <= 0
				return Validity.NO_FREE_UNIT_SLOT
		else
			return Validity.INVALID_TARGET
	else if abilityId == AbilityIdsCustom.removeWorker
		// Check if source is valid:
		let sourceWorkplace = this.getWorkplace()
		if sourceWorkplace == null
			error("No workplace type")
		else if sourceWorkplace instanceof Workshop
			// Check if source has workers:
			if (sourceWorkplace castTo Workshop).getWorkerCount() <= 0
				return Validity.WORKERS_NOT_AVAILABLE
		else if sourceWorkplace instanceof Gatherplace
			// Check if source has workers:
			if (sourceWorkplace castTo Gatherplace).getWorkerCount() <= 0
				return Validity.WORKERS_NOT_AVAILABLE
		else
			error("Casting building is no workshop or gatherplace (" + this.getName() + ")")
			return Validity.ERROR
	else if abilityId == AbilityIdsCustom.changeProduct
		// Check if source is valid:
		let sourceWorkplace = this.getWorkplace()
		if sourceWorkplace == null
			error("No workplace type")
		else if sourceWorkplace instanceof Workshop
			DoNothing()
		else if sourceWorkplace instanceof Gatherplace
			DoNothing()
		else
			error("Casting building is no workshop or gatherplace (" + this.getName() + ")")
			return Validity.ERROR
	else if abilityId == AbilityIdsCustom.targetDestructable
		// Check if target is valid:
		if target.userDest == null or target.userUnit != null
			return Validity.INVALID_TARGET
	else
		// Unregistered abilityId:
		return Validity.UNREGISTERED
	// No validity check failed:
	return Validity.VALID

/** Returns if casting given ability by the unit on given target is valid. */
public function unit.isAbilityValid(int abilityId, entity target) returns bool
	return this.getAbilityValidity(abilityId, target) == Validity.VALID

init
	// Register abilities being cast:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST) ->
		let source = GetTriggerUnit()
		let target = entity(GetSpellTargetUnit(), GetSpellTargetDestructable())
		let abilityId = GetSpellAbilityId()
		let sourceUnitId = source.getUnitId()
		if sourceUnitId != DUMMY_CASTER_UNIT_ID and sourceUnitId != DUMMY_UNIT_ID  // Ignore dummy orders
			// Cancel current target order for clean cast validity and ordering:
			let orderSource = source.getOrderSource()
			if orderSource == null
				Log.warn("WARNING: " + source.getName() + " cast " + abilityId.getOrderName() + " on " + target.getName() + " without being registered as an OrderSource")
			else
				orderSource.cancel()
			// Check ability validity:
			let val = source.getAbilityValidity(abilityId, target)
			if val == Validity.VALID
				switch abilityId
					case AbilityIdsCustom.transferWares
						// Transfer wares from source to target:
						source.transferWares(target.assertUnit())
					case AbilityIdsCustom.build
						// Add worker to building site:
						let targetBuildingSite = target.assertUnit().getBuildingSite()
						CallbackSimple invalidateCb = () ->
							doAfter(MIN_TRIGGER_SLEEP_TIME) ->
								let nextBuildingSite = source.findNextBuildingSite()
								if nextBuildingSite != null
									source.issueTargetAbilityOrder(AbilityIdsCustom.build, nextBuildingSite, true)
						CallbackSimple cancelCb = () ->
							targetBuildingSite.removeWorker(source)
						source.setOrder(AbilityIdsCustom.build, target, new OrderAction(invalidateCb, cancelCb))
						targetBuildingSite.addWorker(source)
					case AbilityIdsCustom.harvest
						// Perform harvest after ability is finished:
						let targetResource = target.getResource(true)
						CallbackSimple invalidateCb = () ->
							var nextResource = source.findNextAllowedResource(MAX_AUTO_ORDER_RANGE)  // Look for new resource in range of worker
							if nextResource.isEmpty()
								nextResource = source.findNextAllowedResource(MAX_AUTO_ORDER_RANGE, target.getPos())  // Look for new resource in range of last target
							if nextResource.isEmpty() == false
								source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, nextResource.getWidget(), true)  // Auto-recast on new target
							else
								printTimedToPlayer("Warning: Worker has no resource in range anymore", INFO_MSG_TIME, source.getOwner())
								source.abortOrder()
						CallbackSimple cancelCb = () ->
							targetResource.removeWorker()
						CallbackSimple finishCb = () ->
							targetResource.removeWorker()
							if source.harvest(target)
								source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, target.getWidget(), true)  // Auto-recast on same target
							else
								let nextResource = source.findNextAllowedResource(MAX_AUTO_ORDER_RANGE)  // Look for new resource in range of worker
								if nextResource.isEmpty() == false
									source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, nextResource.getWidget(), true)  // Auto-recast on new target
								else
									printTimedToPlayer("Warning: Worker has no resource in range anymore", INFO_MSG_TIME, source.getOwner())
									source.abortOrder()
						source.setOrder(AbilityIdsCustom.harvest, target, new OrderAction(invalidateCb, cancelCb, finishCb, AbilityData_Harvest.cooldown, null))
						targetResource.addWorker()
					case AbilityIdsCustom.enterBuilding
						let targetUnit = target.assertUnit()
						let targetHome = targetUnit.getHome()
						let targetWorkplace = targetUnit.getWorkplace()
						if targetHome != null
							// Add source to target home as inhabitant or enter it:
							let sourcePerson = source.getPerson()
							if targetHome.isInhabitant(source)
								sourcePerson.enterBuilding(targetUnit)
							else
								sourcePerson.setHomeBuilding(targetHome)
						else if targetWorkplace == null
							error("No workplace type")
						else if targetWorkplace instanceof Workshop
							let targetWorkshop = (targetWorkplace castTo Workshop)
							// Transfer educts, add source to target workshop as worker and enter it:
							let sourcePerson = source.getPerson()
							targetWorkshop.transferWares(source.getStorage(), false)
							if targetWorkshop.isWorker(source) == false
								sourcePerson.setWorkplaceBuilding(targetWorkshop)
							sourcePerson.enterBuilding(targetUnit)
						else if targetWorkplace instanceof Gatherplace
							let targetGatherplace = (targetWorkplace castTo Gatherplace)
							// Transfer wares, add source to target gatherplace as worker and order to work:
							targetGatherplace.transferWares(source.getStorage(), false)
							if targetGatherplace.isWorker(source) == false
								source.getPerson().setWorkplaceBuilding(targetGatherplace)
							targetGatherplace.orderWorking(source)
						else if targetWorkplace instanceof Depository
							let targetDepository = (targetWorkplace castTo Depository)
							// Transfer wares, add source to target depository as worker and order to work:
							targetDepository.transferWares(source.getStorage(), false)
							if targetDepository.isWorker(source) == false
								source.getPerson().setWorkplaceBuilding(targetDepository)
							targetDepository.orderWorking(source, true)
					case AbilityIdsCustom.removeWorker
						let sourceWorkplace = source.getWorkplace()
						if sourceWorkplace == null
							error("No workplace type")
						else if sourceWorkplace instanceof Workshop
							// Remove a worker from source workshop:
							let person = (sourceWorkplace castTo Workshop).getFirstWorker().getPerson()
							if person.getEnteredBuilding() == source
								person.leaveBuilding()
							person.setWorkplaceBuilding(null)
						else if sourceWorkplace instanceof Gatherplace
							// Remove a worker from source gatherplace:
							let person = (sourceWorkplace castTo Gatherplace).getFirstWorker().getPerson()
							person.setWorkplaceBuilding(null)
					case AbilityIdsCustom.changeProduct
						let sourceWorkplace = source.getWorkplace()
						if sourceWorkplace == null
							error("No workplace type")
						else if sourceWorkplace instanceof Workshop
							// Cancel current production and change it to the next variant:
							(sourceWorkplace castTo Workshop).changeProduct()
						else if sourceWorkplace instanceof Gatherplace
							// Cancel orders on current targets and change it to the next variant:
							(sourceWorkplace castTo Gatherplace).changeProduct()
					default
						error("Unmapped ability (" + abilityId.getOrderName() + ")")
			else if val != Validity.UNREGISTERED
				source.reactOnInvalidAbility(abilityId, val)
	// Register issued ability orders:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER) ->
		let issuedOrderId = GetIssuedOrderId()
		let source = GetOrderedUnit()
		// Check for custom ability:
		let orderName = issuedOrderId.getOrderName(source, true)
		if orderName != null
			// Override registered order in OrderSystem to add fitting OrderAction and target handling (works because through the dependency this function is called afterwards):
			let abilityId = orderName.getOrderNameAbility()
			let target = entity(GetOrderTargetUnit(), GetOrderTargetDestructable())
			var val = source.getAbilityValidity(abilityId, target)
			if val == Validity.VALID
				switch orderName
					case "harvest"
						let targetResource = target.getResource(true)
						CallbackSimple invalidateCb = () ->
							let nextResource = source.findNextAllowedResource(MAX_AUTO_ORDER_RANGE)
							if nextResource.isEmpty() == false
								source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, nextResource.getWidget(), true)
						CallbackSimple cancelCb = () ->
							targetResource.removeWorker()
						source.setOrder(issuedOrderId, target, new OrderAction(invalidateCb, cancelCb))
						targetResource.addWorker()  // Reserve worker slot to avoid that too many workers target the same resource
					case "build"
						CallbackSimple invalidateCb = () ->
							let nextBuildingSite = source.findNextBuildingSite()
							if nextBuildingSite != null
								source.issueTargetAbilityOrder(AbilityIdsCustom.build, nextBuildingSite, true)
						source.setOrder(issuedOrderId, target.assertUnit(), new OrderAction(invalidateCb, null))
					case "target destructable"
						if source.hasAbility(AbilityIdsCustom.harvest)
							val = source.getAbilityValidity(AbilityIdsCustom.harvest, target)
							if val == Validity.VALID
								source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, target.getWidget(), true)
							else if val == Validity.NO_FREE_UNIT_SLOT
								let nextResource = source.findNextAllowedResource(MAX_AUTO_ORDER_RANGE, target.getPos())
								if nextResource.isEmpty() == false
									source.issueTargetAbilityOrder(AbilityIdsCustom.harvest, nextResource.getWidget(), true)
								else
									printTimedToPlayer("Invalid: No free resource in range", INFO_MSG_TIME, source.getOwner())
									source.abortOrder()
							else
								source.reactOnInvalidAbility(AbilityIdsCustom.harvest, val)
						else
							source.issueTargetOrder("attack", target.getWidget())
			else if val != Validity.UNREGISTERED
				source.reactOnInvalidAbility(abilityId, val)
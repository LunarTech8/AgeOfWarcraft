/*-------------------------------------------------------------
// Version number:
1.01

// Description:
Allows buildings to require wares as building cost before the building process can be started. Also allows for worker and building type dependant build speed.
Uses the upgrade mechanism from a general building ground to have a fully controllable building progress.
Dynamically adjustes the building health stats based on the building progress.

// Changelog:
- 1.01:
Initial version.
-------------------------------------------------------------*/
package BuildingSystem
import RegisterEvents
import InstantDummyCaster
import OnUnitEnterLeave
import ClosureTimers
import LinkedList
import HashMap
import Icons
import ConstantDataSystem
import MultiboardPrioritySystem
import UtilityFunctions
import Stock
import Unit_TownHall


// --------------------
// Data code
// --------------------

let buildAdjustmentRate = 0.1
let buildStartHPPercentage = 0.1  // Has to be smaller than 1 and bigger than 0
let maxAutoOrderRange = 1024.
let buildingGroundUnitId = UnitIdsCustom.buildingGround
let buildProgressIconPath = Icons.bTNEngineeringUpgrade
let buildTimeLeftIconPath = Icons.bTNStatUp
let workersIconPath = Icons.bTNBash

function getRequiredWares(int buildingUnitId) returns LinkedList<ware>
	var wares = new LinkedList<ware>()
	switch buildingUnitId
		case UnitIdsCustom.farm
			wares.add(ware(WareType.Wood, 4))
		case UnitIdsCustom.townHall
			wares.add(ware(WareType.Wood, 4))
			wares.add(ware(WareType.Stone, 3))
	return wares

function getUnitWorkPower(int workerUnitId, int buildingUnitId) returns real
	switch workerUnitId
		case UnitIdsCustom.peasant
			switch buildingUnitId
				case UnitIdsCustom.farm
					return 0.05
				case UnitIdsCustom.townHall
					return 0.01
	return 0.

function getBuildingScaling(int buildingUnitId) returns real
	switch buildingUnitId
		case UnitIdsCustom.farm
			return 1.
		case UnitIdsCustom.townHall
			return UnitData_Townhall.scalingValue
	return 0.

	
// --------------------
// Functional code
// --------------------

let buildingSiteMap = new IterableMap<unit, BuildingSite>  // TODO: maybe make a general unit to class hash map system with corresponding utility functions
let workerMap = new HashMap<unit, BuildingSite>  // TODO: maybe make a general unit to class hash map system with corresponding utility functions

class BuildingSite extends Stock
	private unit building
	private real fullHP
	private group workers
	private real workProgress
	private real workPower
	private int upgradeProgress

	construct(unit building, real fullHP, LinkedList<ware> requiredWares)
		super(requiredWares)
		this.building = building
		this.fullHP = fullHP
		workers = CreateGroup()
		workProgress = 0.
		workPower = 0.
		upgradeProgress = 1

	ondestroy
		for worker in workers
			removeWorker(worker)
			doAfter(minTriggerSleepTime, () -> worker.issueTargetAbilityOrder(AbilityIdsCustom.build, worker.getPos().findNextBuildingSite(worker.getOwner()), true))
		workers.destr()

	function hasNoRequiredWaresLeft() returns bool
		return wares.isEmpty()

	function getWorkProgress() returns real
		return workProgress	

	function getWorkPower() returns real
		return workPower

	function getWorkerCount() returns int
		return workers.size()
	
	function addWorker(unit worker)
		if workerMap.has(worker) == false and workers.contains(worker) == false
			workerMap.put(worker, this)
			workPower += getUnitWorkPower(worker.getUnitId(), building.getUnitId())
			workers.addUnit(worker)
			InstantDummyCaster.castTarget(null, AbilityIdsCustom.addBuildingBuff, 1, AbilityIdsCustom.addBuildingBuff.getAbilityOrder(), worker)
			doAfter(minTriggerSleepTime, () -> SetUnitAnimation(worker, "work"))
		else
			Log.error("ERROR: can't add worker to this building site")

	function removeWorker(unit worker)
		if workerMap.get(worker) == this and workers.contains(worker)
			workerMap.remove(worker)
			workPower -= getUnitWorkPower(worker.getUnitId(), building.getUnitId())
			workers.removeUnit(worker)
			ResetUnitAnimation(worker)
			worker.removeAbility(BuffIdsCustom.building)
		else
			Log.error("ERROR: can't remove worker from this building site")
	
	function adjustProgress() returns bool
		if workPower > 0.
			// Adjust work progress:
			workProgress += workPower * buildAdjustmentRate
			if workProgress >= 1.
				// Finish construction:
				UnitSetUpgradeProgress(building, 100)
				return true
			else
				// Recalculate values:
				building.setMaxHP(max(1, (fullHP * (workProgress * (1 - buildStartHPPercentage) + buildStartHPPercentage)).toInt()))
				building.addHP(fullHP * (1 - buildStartHPPercentage) * workPower * buildAdjustmentRate)
				upgradeProgress = min(max(1, (workProgress * 100.).toInt()), 99)
		// Always adjust upgrade progress so that it stays correct:
		UnitSetUpgradeProgress(building, upgradeProgress)
		return false


function unit.addBuildingSite()
	if buildingSiteMap.has(this) == false
		let targetUnitId = this.getUnitId()
		let fullHP = this.getMaxHP()
		let buildingGround = this.replace(buildingGroundUnitId)
		IssueImmediateOrderById(buildingGround, targetUnitId.getUnitTypeId())  // Upgrade building ground to target building type
		buildingGround.setScale(getBuildingScaling(targetUnitId))
		buildingGround.setMaxHP(max(1, (fullHP * buildStartHPPercentage).toInt()))
		let buildingSite = new BuildingSite(buildingGround, fullHP, getRequiredWares(targetUnitId))
		buildingSiteMap.put(buildingGround, buildingSite)
		if buildingSite.hasNoRequiredWaresLeft()
			// Order nearby idle workers to build on building site:
			ENUM_GROUP.enumUnitsInRange(buildingGround.getPos(), maxAutoOrderRange, null)
			let owner = buildingGround.getOwner()
			for worker from ENUM_GROUP
				if targetUnitId.isValidWorkerType(worker.getUnitId()) and worker.getOwner() == owner and (worker.getCurrentOrder() == 0 or IsUnitSelected(worker, owner)) and worker.hasAbility(BuffIdsCustom.building) == false
					worker.issueTargetAbilityOrder(AbilityIdsCustom.build, buildingGround, true)

function unit.removeBuildingSite()
	if buildingSiteMap.has(this)
		destroy buildingSiteMap.get(this)
		buildingSiteMap.remove(this)

function vec2.findNextBuildingSite(player owner) returns unit
	var filter = Filter() ->
		var conditionsFulfilled = false
		let buildingSite = GetFilterUnit().getBuildingSite()
		// Only allow building sites that don't have any required wares left:
		if buildingSite != null and buildingSite.hasNoRequiredWaresLeft()
			conditionsFulfilled = true
		return conditionsFulfilled
	let building = this.getNearestUnit(maxAutoOrderRange, filter, owner)
	filter.destr()
	return building

/** If worker is building stop him. */
function unit.stopBuilding()
	if workerMap.has(this)
		workerMap.get(this).removeWorker(this)

/** Returns the building site of given unit. */
public function unit.getBuildingSite() returns BuildingSite
	if buildingSiteMap.has(this)
		return buildingSiteMap.get(this)
	else
		return null

/** Returns the building site of given worker. */
public function unit.getBuildingSiteOfWorker() returns BuildingSite
	if workerMap.has(this)
		return workerMap.get(this)
	else
		return null
		
/** Returns if the given worker type can work on given building type */
public function int.isValidWorkerType(int workerUnitId) returns bool
	return getUnitWorkPower(workerUnitId, this) > 0.

/** Sets system relevant infos for given object in target multiboard. */
public function multiboard.setObjectInfoMultiboard(BuildingSite bs) returns bool
	this.addMultiboardLine("Building site:", headlineIconPath)
	this.addMultiboardLine("Build progress: " + (bs.getWorkProgress() * 100.).toInt().toString() + "%", buildProgressIconPath)
	let workPower = bs.getWorkPower()
	if workPower > 0.
		this.addMultiboardLine("Build time left: " + ((1 - bs.getWorkProgress()) / workPower).toInt().toString() + "s", buildTimeLeftIconPath)
		this.addMultiboardLine("Workers: " + bs.getWorkerCount().toString(), workersIconPath)
	let amountOfWareTypes = bs.getAmountOfWareTypes()
	if amountOfWareTypes > 0
		this.addMultiboardLine("Required wares:", headlineIconPath)
		for i = 0 to (amountOfWareTypes - 1)
			var ware = bs.getWare(i)
			this.addMultiboardLine(ware.count.toString() + " " + ware.toString(true), ware.getIconPath())
	return true
	
init
	// Remove resource from every appropriate removed unit:
	onLeave() -> 
		getEnterLeaveUnit().removeBuildingSite()
	// Adjust progress of all building sites:
	let finishedBuildings = CreateGroup()
	doPeriodically(buildAdjustmentRate) cb ->
		for building in buildingSiteMap
			if buildingSiteMap.get(building).adjustProgress()
				finishedBuildings.addUnit(building)
		// Remove finished building sites:
		for building from finishedBuildings
			building.removeBuildingSite()
	// Register construction starts:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_CONSTRUCT_START) ->
		let building = GetConstructingStructure()
		if getBuildingScaling(building.getUnitId()) > 0.
			doAfter(minTriggerSleepTime, () -> building.addBuildingSite())
	// Register construction abortions:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_CANCEL) ->
		let building = GetTriggerUnit()
		building.removeBuildingSite()
		building.kill()
		building.remove()
	// Register building stops:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, () -> GetTriggerUnit().stopBuilding())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, () -> GetTriggerUnit().stopBuilding())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> GetTriggerUnit().stopBuilding())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER, () -> GetTriggerUnit().stopBuilding())
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ATTACKED, () -> GetTriggerUnit().stopBuilding())
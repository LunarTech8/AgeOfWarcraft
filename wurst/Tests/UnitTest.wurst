/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Ingame test alternative to the Wurstunit package with additional functionality.

// Examples:
testDebug("test<SystemName>_<FunctionName>", "Debug message")  // DEBUG: only for testing

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package UnitTest
import HashMap


// --------------------
// Data code
// --------------------

let SHOW_START_TEST_MESSAGE = true
let TEST_NAME_WURSTUNIT = "UseWurstunit"


// --------------------
// Functional code
// --------------------

let TEST_NAME_MAP = new HashMap<string, int>

function useTestName(string testName) returns string
	var testNameCounter = 1
	if TEST_NAME_MAP.has(testName)
		testNameCounter = TEST_NAME_MAP.get(testName) + 1
	else if SHOW_START_TEST_MESSAGE
		printLog(Loglevel.TRACE, "[Start test for: " + testName + "]")
	TEST_NAME_MAP.put(testName, testNameCounter)
	return testName + " " + testNameCounter.toString()

public function setTestName(string testName, bool isInGameTest) returns string
	if isInGameTest
		return testName
	else
		return TEST_NAME_WURSTUNIT

public function testSuccess(string testName)
	if testName == TEST_NAME_WURSTUNIT
		testSuccess()
	else
		printLog(Loglevel.INFO, "[" + useTestName(testName) + "] " + "Test successful")

public function testFail(string testName, string msg)
	if testName == TEST_NAME_WURSTUNIT
		testFail(msg)
	else
		printLog(Loglevel.ERROR, "[" + useTestName(testName) + "] " + msg)

public function testDebug(string testName, string msg)
	if testName == TEST_NAME_WURSTUNIT
		println(msg)
	else
		printLog(Loglevel.WARNING, "[" + useTestName(testName) + "] " + msg)
public function testDebug(string testName, int msg)
	testDebug(testName, msg.toString())

/**
 * Asserts that two ints are equal. If they are not, an error is thrown with a message.
 */
public function int.assertEquals(string testName, int expected)
	if this != expected
		testFail(testName, "Expected <" + expected.toString() + ">, Actual <" + this.toString() + ">")

/**
 * Asserts that the passed int is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function int.assertGreaterThan(string testName, int less)
	if this <= less
		testFail(testName, "Expected <" + this.toString() + "> to be greater than <" + less.toString() + ">")

/**
 * Asserts that the passed int is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function int.assertGreaterThanOrEqual(string testName, int less)
	if this < less
		testFail(testName, "Expected <" + this.toString() + "> to be greater than or equal to <" + less.toString() + ">")

/**
 * Asserts that the passed int is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function int.assertLessThan(string testName, int greater)
	if this >= greater
		testFail(testName, "Expected <" + this.toString() + "> to be less than <" + greater.toString() + ">")

/**
 * Asserts that the passed int is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function int.assertLessThanOrEqual(string testName, int greater)
	if this > greater
		testFail(testName, "Expected <" + this.toString() + "> to be less than or equal to <" + greater.toString() + ">")

/**
 * Asserts that two strings are equal. If they are not, an error is thrown with a message.
 */
public function string.assertEquals(string testName, string expected)
	if this != expected
		testFail(testName, "Expected <" + expected + ">, Actual <" + this + ">")

/**
 * Asserts that two reals are equal. If they are not, an error is thrown with a message.
 */
public function real.assertEquals(string testName, real expected)
	if this != expected
		testFail(testName, "Expected <" + expected.toString() + ">, Actual <" + this.toString() + ">")

/**
 * Asserts that the passed real is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function real.assertGreaterThan(string testName, real less)
	if this <= less
		testFail(testName, "Expected <" + this.toString() + "> to be greater than <" + less.toString() + ">")

/**
 * Asserts that the passed real is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function real.assertGreaterThanOrEqual(string testName, real less)
	if this < less
		testFail(testName, "Expected <" + this.toString() + "> to be greater than or equal to <" + less.toString() + ">")

/**
 * Asserts that the passed real is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function real.assertLessThan(string testName, real greater)
	if this >= greater
		testFail(testName, "Expected <" + this.toString() + "> to be less than <" + greater.toString() + ">")

/**
 * Asserts that the passed real is greater than the parameter. If they are not, an error is thrown with a message.
 */
public function real.assertLessThanOrEqual(string testName, real greater)
	if this > greater
		testFail(testName, "Expected <" + this.toString() + "> to be less than or equal to <" + greater.toString() + ">")


/**
 * Asserts that two reals are equal to within a positive delta. If they are not, an error is thrown
 * with a message.
 */
public function real.assertEquals(string testName, real expected, real delta)
	if (this - expected > delta) or (expected - this > delta)
		testFail(testName, "Expected <" + expected.toString() + ">, Actual <" + this.toString() + " with delta " + delta.toString() + ">")

/**
 * Asserts that two booleans are equal. If they are not, an error is thrown with a message.
 */
public function bool.assertEquals(string testName, bool expected)
	if this != expected
		testFail(testName, "Expected <" + expected.toString() + ">, Actual <" + this.toString() + ">")

/** Asserts that this booleans is true. If it is not, an error is thrown with a message. */
public function bool.assertTrue(string testName)
	this.assertEquals(testName, true)

/** Asserts that this booleans is false. If it is not, an error is thrown with a message. */
public function bool.assertFalse(string testName)
	this.assertEquals(testName, false)

/** Asserts that this booleans is true. If it is not, an error is thrown. */
public function assertTrue(string testName, bool b)
	if not b
		testFail(testName, "Assertion failed")

/** Asserts that this booleans is true. If it is not, an error is thrown with the supplied message. */
public function assertTrue(string testName, bool b, string msg)
	if not b
		testFail(testName, "Assertion failed: " + msg)

/** Asserts that the reference t is not null. */
public function assertNotNull<T>(string testName, T t)
	if t == null
		testFail(testName, "Assertion failed: Reference was null.")

/** Asserts that the references a and b are pointing to the same target. */
public function assertEquals<T>(string testName, T a, T b)
	if a != b
		testFail(testName, "Assertion failed: References were not equal.")
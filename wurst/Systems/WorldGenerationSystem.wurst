/*-------------------------------------------------------------
// Version number:
1.01

// Description:
Handles world map generation like terrain deformation and unit placement.

// Changelog:
- 1.01:
Initial version.
-------------------------------------------------------------*/
package WorldGenerationSystem
import TerrainUtils
import Interpolation
import ClosureTimers
import Stock
import Array2D
import ConstantDataSystem
import StorageSystem
import UtilityFunctions


// --------------------
// Data code
// --------------------

// Terrain:
let TILES_X = 16
let TILES_Y = 16
let TILE_DISTANCE = 128.
let MIN_TERRAIN_HEIGHT = -128.
let MAX_TERRAIN_HEIGHT = 256.

// General:
let ORIGIN_VEC = vec2(-0.5 * TILES_X, -0.5 * TILES_Y) * TILE_DISTANCE
let USER_PLAYER = Player(0)

// Player units:
let START_VEC = vec2(0., 0.)
let START_PEASANT_COUNT = 5
let START_WOOD_COUNT_PER_PEASANT = 5

// Nature:
let NATURE_PLAYER = Player(PLAYER_NEUTRAL_PASSIVE)


// --------------------
// Functional code
// --------------------

string currentInitStatus = null

function newInitStatus(string statusName)
    if currentInitStatus != null
        print("Initialisation: Finished " + currentInitStatus)
    currentInitStatus = statusName
    if currentInitStatus != null
        print("Initialisation: Starting " + currentInitStatus)

function generateWorld()
    // Create terrain:
    newInitStatus("terrain creation")
    Array2D<real> heightMap = generatePerlinNoise(TILES_X, TILES_Y, 5, 0.6, "Cosine")
    for	x = 0 to TILES_X - 1
        for	y = 0 to TILES_Y - 1
            ORIGIN_VEC.add(x * TILE_DISTANCE, y * TILE_DISTANCE).addTerrainHeight(linear(MIN_TERRAIN_HEIGHT, MAX_TERRAIN_HEIGHT, heightMap.get(x, y)))
    TriggerSleepAction(MIN_TRIGGER_SLEEP_TIME)
    // Create player units:
    newInitStatus("player units creation")
    for i = 1 to START_PEASANT_COUNT
        var u = createUnit(USER_PLAYER, UnitIdsCustom.peasant, START_VEC, angle(0.))
        nullTimer() ->
            let storage = u.getStorage()
            if storage != null
                storage.addWare(ware(WareType.Wood, START_WOOD_COUNT_PER_PEASANT))
    createUnit(USER_PLAYER, UnitIdsCustom.footman, START_VEC, angle(0.))
    // Create trees:
    newInitStatus("trees creation")
    Array2D<real> treeMap = generatePerlinNoise(TILES_X, TILES_Y, 5, 0.6, "Cosine")
    for	x = 0 to TILES_X - 1
        for	y = 0 to TILES_Y - 1
            if treeMap.get(x, y) >= GetRandomReal(0., 1.)
                var v = ORIGIN_VEC + vec2(x + GetRandomReal(-0.5, 0.5), y + GetRandomReal(-0.5, 0.5)) * TILE_DISTANCE
                createUnit(NATURE_PLAYER, UnitIdsCustom.beech.getUnitTypeId(), v, getRandomAngle())
    // Initialisation finished:
    newInitStatus(null)
    print("Finished initialisation")

init
    // Use a trigger to allow the use of TriggerSleepAction:
    // TEST: following lines are deactivated for StartMap development
    // CreateTrigger()
    // ..addAction(function generateWorld)
    // ..execute()
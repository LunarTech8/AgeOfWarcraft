/*-------------------------------------------------------------
// Version number:
1.01

// Description:
Gives predefined unit types resources wares that can be extracted by workers via the harvest ability.

// Changelog:
- 1.01:
Initial version.
-------------------------------------------------------------*/
package ResourceSystem
import RegisterEvents
import LinkedList
import HashMap
import ConstantDataSystem
import MultiboardPrioritySystem
import StorageSystem
import UtilityFunctions
import OnUnitEnterLeave
import ClosureTimers
import Ability_Harvest


// --------------------
// Data code
// --------------------

let countPlayers = 12  // Highest player slot in use
let infoMsgTime = 2.
let autoCastDelay = 0.25

function getDefaultInitWares(int unitId) returns LinkedList<ware>
	var wares = new LinkedList<ware>()
	switch unitId
		case UnitIdsCustom.beech
			wares.add(ware(WareType.Wood, GetRandomInt(6, 8)))
		case UnitIdsCustom.birch
			wares.add(ware(WareType.Wood, GetRandomInt(4, 6)))
		case UnitIdsCustom.greyOak
			wares.add(ware(WareType.Wood, GetRandomInt(6, 8)))
		case UnitIdsCustom.chestnut
			wares.add(ware(WareType.Wood, GetRandomInt(4, 6)))
	return wares

function isKilledWhenDepleted(int unitId) returns bool
	switch unitId
		case UnitIdsCustom.beech
			return true
		case UnitIdsCustom.birch
			return true
		case UnitIdsCustom.greyOak
			return true
		case UnitIdsCustom.chestnut
			return true
		default
			return false

function getWorkProgressPerHarvest(int workerUnitId, int resourceUnitId) returns real
	switch resourceUnitId
		case UnitIdsCustom.beech
			switch workerUnitId
				case UnitIdsCustom.peasant
					return 0.67
			return 0.5
		case UnitIdsCustom.birch
			return 0.5
		case UnitIdsCustom.greyOak
			return 0.5
		case UnitIdsCustom.chestnut
			return 0.5
		default
			return 0.

function getWaresPerFinishedWork(int unitId) returns LinkedList<ware>
	var wares = new LinkedList<ware>()
	switch unitId
		case UnitIdsCustom.beech
			wares.add(ware(WareType.Wood, 1))
		case UnitIdsCustom.birch
			wares.add(ware(WareType.Wood, 1))
		case UnitIdsCustom.greyOak
			wares.add(ware(WareType.Wood, 1))
		case UnitIdsCustom.chestnut
			wares.add(ware(WareType.Wood, 1))
	return wares
			
	
// --------------------
// Functional code
// --------------------

let resourceMap = new HashMap<unit, Resource>  // TODO: maybe make a general unit to class hash map system with corresponding utility functions

class Resource
	private unit resourceUnit
	private bool isKilledWhenDepleted
	private real workProgress
	private LinkedList<ware> wares

	construct(unit resourceUnit, bool isKilledWhenDepleted, LinkedList<ware> initWares)
		this.resourceUnit = resourceUnit
		this.isKilledWhenDepleted = isKilledWhenDepleted
		workProgress = 0.
		wares = initWares

	ondestroy
		destroy wares

	function getWare(int index) returns ware
		if index >= wares.size()
			Log.error("ERROR: Index has to be smaller than list size")
		return wares.get(index)
		
	function getAmountOfWareTypes() returns int
		return wares.size()

	function getWareCount(WareType wareType) returns int
		for iWare in wares
			if iWare.wareType == wareType
				return iWare.count
		return 0
	
	function removeWare(ware ware)
		for iWare in wares
			if iWare.hasSameType(ware)
				if iWare.count <= ware.count
					wares.remove(iWare)
				else
					wares.replace(iWare, iWare.changedCopy(-ware.count))

	function work(real workProgress) returns bool
		this.workProgress += workProgress
		if this.workProgress >= 1.
			this.workProgress -= 1.
			return true
		else
			return false

	function checkForDepletionKill() returns bool
		if isKilledWhenDepleted and wares.isEmpty()
			return true
		return false


function addResource(unit targetUnit)
	if resourceMap.has(targetUnit) == false
		var unitType = getUnitId(targetUnit)
		var wares = getDefaultInitWares(unitType)
		if wares.isEmpty()
			destroy wares
		else
			var resource = new Resource(targetUnit, isKilledWhenDepleted(unitType), wares)
			resourceMap.put(targetUnit, resource)
	
function removeResource(unit targetUnit)
	if resourceMap.has(targetUnit)
		destroy resourceMap.get(targetUnit)
		resourceMap.remove(targetUnit)

/** Returns harvest validity: 1=Valid 2=InvalidWorker 3=InvalidTarget 4=NoFreeStorage 5=ResourceIsDepleted */
function getHarvestValidity(unit workerUnit, unit targetUnit) returns int
	// Check if source and target are valid:
	var storage = getStorage(workerUnit)
	if storage == null
		return 2
	var resource = getResource(targetUnit)
	if resource == null
		return 3
	// Check if wares can be harvested:
	if storage.getFreeCapacity() <= 0
		return 4
	else if resource.getAmountOfWareTypes() <= 0
		return 5
	else
		return 1
		
/** Perform harvest and returns if another harvest with the given target is still valid. */
function harvest(unit worker, unit target) returns bool
	var storage = getStorage(worker)
	var resource = getResource(target)
	let resourceUnitId = getUnitId(target)
	// Work resource:
	if resource.work(getWorkProgressPerHarvest(getUnitId(worker), resourceUnitId))
		// Transfere wares:
		let wares = getWaresPerFinishedWork(resourceUnitId)
		for ware in wares
			// Check resource count:
			let resourceWareCount = resource.getWareCount(ware.wareType)
			if resourceWareCount > 0
				// Limit by resource count:
				if resourceWareCount < ware.count
					ware.count = resourceWareCount
				// Add ware to storage:
				var excess = storage.addWare(ware)
				// Remove ware from resource:
				if excess < ware.count
					ware.count -= excess
					resource.removeWare(ware)
		destroy wares
		// Check for resource depletion:
		if resource.checkForDepletionKill()
			target.kill()
			return false
	return getHarvestValidity(worker, target) == 1
		
/** Returns the resource of given unit. */
public function getResource(unit targetUnit) returns Resource
	if resourceMap.has(targetUnit)
		return resourceMap.get(targetUnit)
	else
		return null

/** Sets system relevant infos for given object in target multiboard. */
public function setObjectInfoMultiboard(multiboard targetMultiboard, Resource resource) returns bool
	addMultiboardLine(targetMultiboard, "Resource:", BaseIconType.Headline.getIconPath())
	for i = 0 to (resource.getAmountOfWareTypes() - 1)
		var ware = resource.getWare(i)
		addMultiboardLine(targetMultiboard, ware.count.toString() + " " + ware.toString(true), ware.getIconPath())
	return true
	
init
	// Add resource to every appropriate new unit:
	onEnter() -> 
		addResource(getEnterLeaveUnit())
	// Remove resource from every appropriate removed unit:
	onLeave() -> 
		removeResource(getEnterLeaveUnit())
	// Register harvest spell:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST) ->
		if GetSpellAbilityId() == AbilityIdsCustom.harvest
			let worker = GetTriggerUnit()
			let target = GetSpellTargetUnit()
			let validity = getHarvestValidity(worker, target)
			if validity != 1
				// React on invalid harvest command:
				switch validity
					case 2  // InvalidWorker
						printTimedToPlayer("Invalid: No storage for harvest", infoMsgTime, worker.getOwner())
					case 3  // InvalidTarget
						printTimedToPlayer("Invalid: Harvest target is no resource", infoMsgTime, worker.getOwner())
					case 4  // NoFreeStorage
						printTimedToPlayer("Invalid: No free storage for harvest", infoMsgTime, worker.getOwner())
					case 5  // ResourceIsDepleted
						printTimedToPlayer("Invalid: Harvest target is already depleted", infoMsgTime, worker.getOwner())
				worker.abortOrder()
			else
				// Perform harvest after ability casting is finished:
				doAfter(AbilityData_Harvest.cooldown) ->
					if harvest(worker, target)
						// Auto-recast ability:
						worker.issueTargetAbilityOrder(AbilityIdsCustom.harvest, target, true)
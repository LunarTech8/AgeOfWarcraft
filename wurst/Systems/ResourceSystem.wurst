/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Gives predefined unit types resources wares that can be extracted by workers via the harvest ability.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package ResourceSystem
import OnUnitEnterLeave
import ConstantDataSystem
import MultiboardPrioritySystem
import UtilityFunctions
import Stock
import StorageSystem
import UnitMap
import UnitIndexer
import LinkedList
import Icons


// --------------------
// Data code
// --------------------

let MAX_AUTO_ORDER_RANGE = 1024.
let WORKERS_ICON_PATH = Icons.bTNBash

function getDefaultInitWares(int resourceUnitId) returns LinkedList<ware>
	let wares = new LinkedList<ware>()
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			wares.add(ware(WareType.WOOD, GetRandomInt(6, 8)))
		case UnitIdsCustom.Trees.birch
			wares.add(ware(WareType.WOOD, GetRandomInt(4, 6)))
		case UnitIdsCustom.Trees.greyOak
			wares.add(ware(WareType.WOOD, GetRandomInt(6, 8)))
		case UnitIdsCustom.Trees.chestnut
			wares.add(ware(WareType.WOOD, GetRandomInt(4, 6)))
	return wares

function isKilledWhenDepleted(int resourceUnitId) returns bool
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			return true
		case UnitIdsCustom.Trees.birch
			return true
		case UnitIdsCustom.Trees.greyOak
			return true
		case UnitIdsCustom.Trees.chestnut
			return true
		default
			return false

function getWorkProgressPerHarvest(int workerUnitId, int resourceUnitId) returns real
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			switch workerUnitId
				case UnitIdsCustom.Melee.peasant
					return 0.67
			return 0.5
		case UnitIdsCustom.Trees.birch
			return 0.5
		case UnitIdsCustom.Trees.greyOak
			return 0.5
		case UnitIdsCustom.Trees.chestnut
			return 0.5
		default
			return 0.

function getWaresPerFinishedWork(int resourceUnitId) returns LinkedList<ware>
	let wares = new LinkedList<ware>()
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			wares.add(ware(WareType.WOOD, 1))
		case UnitIdsCustom.Trees.birch
			wares.add(ware(WareType.WOOD, 1))
		case UnitIdsCustom.Trees.greyOak
			wares.add(ware(WareType.WOOD, 1))
		case UnitIdsCustom.Trees.chestnut
			wares.add(ware(WareType.WOOD, 1))
	return wares

/** Returns the harvest priority of given resource unit type. Higher values mean a higher priority. */
function getHarvestPriority(int resourceUnitId) returns int
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			return 2
		case UnitIdsCustom.Trees.birch
			return 1
		case UnitIdsCustom.Trees.greyOak
			return 3
		case UnitIdsCustom.Trees.chestnut
			return 2
	return 0

/** Returns the maximum concurrent amount of workers allowed for of given resource unit type. */
function getMaxWorkerCount(int resourceUnitId) returns int
	switch resourceUnitId
		case UnitIdsCustom.Trees.beech
			return 1
		case UnitIdsCustom.Trees.birch
			return 1
		case UnitIdsCustom.Trees.greyOak
			return 2
		case UnitIdsCustom.Trees.chestnut
			return 2
	return 99


// --------------------
// Functional code
// --------------------

let RESOURCE_MAP = new UnitHashMap<Resource>(null)

class Resource extends Stock
	private bool isKilledWhenDepleted
	private real workProgress
	private int workerCount

	construct(unit resource, bool isKilledWhenDepleted, LinkedList<ware> initWares)
		super(initWares)
		user = resource
		this.isKilledWhenDepleted = isKilledWhenDepleted
		workProgress = 0.
		workerCount = 0

	private function work(real workProgress) returns bool
		this.workProgress += workProgress
		if this.workProgress >= 1.
			this.workProgress -= 1.
			return true
		else
			return false

	function getWorkerCount() returns int
		return workerCount

	function getFreeWorkerSlots() returns int
		return getMaxWorkerCount(user.getUnitId()) - workerCount

	/** Perform harvest and returns the harvested wares. */
	function harvest(int workerUnitId) returns LinkedList<ware>
		let resourceUnitId = user.getUnitId()
		// Work resource:
		if work(getWorkProgressPerHarvest(workerUnitId, resourceUnitId))
			// Return harvested wares:
			return getWaresPerFinishedWork(resourceUnitId)
		// No wares harvested yet:
		return null

	function addWorker()
		if workerCount >= getMaxWorkerCount(user.getUnitId())
			Log.error("ERROR: Can not add more workers to resource (" + user.getName() + ")")
		workerCount += 1

	function removeWorker()
		if workerCount <= 0
			Log.error("ERROR: Can not remove more workers from resource (" + user.getName() + ")")
		workerCount -= 1

	function checkForDepletionKill() returns bool
		if isKilledWhenDepleted and wares.isEmpty()
			return true
		return false


function unit.addResource()
	if RESOURCE_MAP.has(this) == false
		let unitId = this.getUnitId()
		let wares = getDefaultInitWares(unitId)
		if wares.isEmpty()
			destroy wares
		else
			RESOURCE_MAP.put(this, new Resource(this, isKilledWhenDepleted(unitId), wares))

function unit.removeResource()
	let resource = RESOURCE_MAP.tryRemove(this)
	if resource != null
		destroy resource

int findNextResource_Priority
constant group findNextResource_ValidUnits = CreateGroup()
/** Returns next valid resource unit for the unit. */
public function unit.findNextResource() returns unit
	// Check if worker has enough free space:
	if this.getStorage().getFreeCapacity() > 0
		let pos = this.getPos()
		// Get valid units in range and highest priority:
		findNextResource_Priority = 0
		let filter = Filter() ->
			var conditionsFulfilled = false
			let filterUnit = GetFilterUnit()
			let resource = filterUnit.getResource()
			let priority = getHarvestPriority(filterUnit.getUnitId())
			// Only allow resources that have wares left, free worker slots and a high enough priority:
			if resource != null and resource.isEmpty() == false and resource.getFreeWorkerSlots() > 0 and priority >= findNextResource_Priority
				findNextResource_Priority = priority
				conditionsFulfilled = true
			return conditionsFulfilled
		ENUM_GROUP.enumUnitsInRange(pos, MAX_AUTO_ORDER_RANGE, filter)
		filter.destr()
		// Remove lower priority units:
		for target from ENUM_GROUP
			if getHarvestPriority(target.getUnitId()) == findNextResource_Priority
				findNextResource_ValidUnits.addUnit(target)
		// Return nearest valid unit:
		return pos.getNearestUnit(findNextResource_ValidUnits)
	else
		return null

/** Returns the resource of given unit. */
public function unit.getResource() returns Resource
	return RESOURCE_MAP.tryGet(this)

/** Returns the count of all resources. */
public function getResourceCount() returns int
	return RESOURCE_MAP.getValueCount()

/** Sets system relevant infos for given object in target multiboard. */
public function multiboard.setObjectInfoMultiboard(Resource r) returns bool
	this.addMultiboardLine("Resource:", HEADLINE_ICON_PATH)
	this.addMultiboardLine("Workers: " + r.getWorkerCount().toString() + "/" + getMaxWorkerCount(r.user.getUnitId()).toString(), WORKERS_ICON_PATH)
	for i = 0 to (r.getAmountOfWareTypes() - 1)
		let ware = r.getWare(i)
		this.addMultiboardLine(ware.count.toString() + " " + ware.toString(true), ware.getIconPath())
	return true

init
	// Add resource to every appropriate new unit:
	onEnter() ->
		getEnterLeaveUnit().addResource()
	// Remove resource from every appropriate removed unit:
	onUnitDeindex() ->
		getIndexingUnit().removeResource()
/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Stores settlements and its buildings, persons and progress level.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package SettlementSystem
import UnitMap
import MultiboardPrioritySystem
import Icons
import UnitIndexer
import RegisterEvents
import Orders
import UtilityFunctions
import ConstantData
import UnitTest
import ClosureTimers
import StorageSystem
import Stock
import ErrorHandling


// --------------------
// Data code
// --------------------

let LEVEL_ICON_PATH = Icons.bTNSnazzyScroll
let POPULATION_ICON_PATH = Icons.bTNPeasant
let BUILDINGS_ICON_PATH = Icons.bTNFarm
let MAX_LEVEL = 3

function getBuildingRange(int level) returns real
	switch level
		case 1
			return 768.
		case 2
			return 1024.
		case 3
			return 1280.
	return 0.

function getLevel(int popCount) returns int
	if popCount >= 30
		return 3
	else if popCount >= 10
		return 2
	return 1

function getRequiredLevel(int buildingId) returns int
	switch buildingId
		case UnitIdsCustom.AoW.townHall
			return 0
		case UnitIdsCustom.AoW.keep
			return 0
		case UnitIdsCustom.AoW.castle
			return 0
		case UnitIdsCustom.AoW.peasantHouse
			return 1
		case UnitIdsCustom.AoW.lumberMill
			return 1
		case UnitIdsCustom.AoW.barracks
			return 1
		case UnitIdsCustom.AoW.blacksmith
			return 2
	return 1

function getInvalidMessageText(Validity val) returns string
	switch val
		case OUTSIDE_RANGE
			return "Can't be constructed outside the range of a settlement"
		case INVALID_LEVEL
			return "Settlement level to low for this building type"
		case TOO_CLOSE
			return "Can't be founded that close to another settlement"
		case ALREADY_ADDED
			return "Is already a settlement"
		case INVALID_TYPE
			return "Invalid building type to found a settlement"
		default
			error("Unregistered invalidity message text for " + (val castTo int).toString())
			return null


// --------------------
// Functional code
// --------------------

let SETTLEMENT_MAP = new IterableUnitHashMap<Settlement>(null)

enum Validity
	VALID
	OUTSIDE_RANGE
	INVALID_LEVEL
	TOO_CLOSE
	ALREADY_ADDED
	INVALID_TYPE

class Settlement
	private unit centreBuilding
	protected group buildings
	protected group persons
	protected int level

	construct(unit centreBuilding)
		this.centreBuilding = centreBuilding
		buildings = CreateGroup()
		persons = CreateGroup()
		updateLevel()

	ondestroy
		for building in buildings
			building.setOwner(Player(PLAYER_NEUTRAL_PASSIVE), true)
		buildings.destr()
		persons.destr()

	private function updateLevel()
		level = getLevel(persons.size())

	function isMember(unit member) returns bool
		if member.isType(UNIT_TYPE_STRUCTURE)
			return buildings.contains(member)
		else
			return persons.contains(member)

	function addMemeber(unit member)
		if member.isType(UNIT_TYPE_STRUCTURE)
			buildings.addUnit(member)
		else
			persons.addUnit(member)
			updateLevel()

	function removeMember(unit member)
		if member.isType(UNIT_TYPE_STRUCTURE)
			buildings.removeUnit(member)
		else
			persons.removeUnit(member)
			updateLevel()


function unit.addSettlement() returns Validity
	// Check validity:
	if SETTLEMENT_MAP.has(this)
		return Validity.ALREADY_ADDED
	if isCentreBuilding(this.getUnitId()) == false
		return Validity.INVALID_TYPE
	let pos = this.getPos()
	let range = 2 * getBuildingRange(MAX_LEVEL)
	for centreBuilding in SETTLEMENT_MAP
		if pos.distanceTo(centreBuilding.getPos()) <= range
			return Validity.TOO_CLOSE
	// Add settlement and nearby units:
	let settlement = new Settlement(this)
	SETTLEMENT_MAP.put(this, settlement)
	ENUM_GROUP.enumUnitsInRange(this.getPos(), getBuildingRange(settlement.level), null)
	let owner = this.getOwner()
	for targetUnit from ENUM_GROUP
		if targetUnit.getOwner() == owner and targetUnit.getSettlement() == null
			settlement.addMemeber(targetUnit)
	return Validity.VALID

function unit.addToSettlementInRange() returns Validity
	let pos = this.getPos()
	for centreBuilding in SETTLEMENT_MAP
		let settlement = SETTLEMENT_MAP.get(centreBuilding)
		if pos.distanceTo(centreBuilding.getPos()) <= getBuildingRange(settlement.level)
			if getRequiredLevel(this.getUnitId()) > settlement.level
				return Validity.INVALID_LEVEL
			settlement.addMemeber(this)
			return Validity.VALID
	return Validity.OUTSIDE_RANGE

function unit.removeFromSettlement()
	var settlement = SETTLEMENT_MAP.tryRemove(this)
	if settlement != null
		destroy settlement  // Destroy settlement if centre building gets removed
	else
		settlement = this.getSettlement()
		if settlement != null
			settlement.removeMember(this)

/** Returns if given unit is a centre buidlding of a settlement. */
public function isCentreBuilding(int buildingId) returns bool
	return getRequiredLevel(buildingId) == 0

/** Returns the settlement of given unit. */
public function unit.getSettlement() returns Settlement
	var settlement = SETTLEMENT_MAP.tryGet(this)  // Check if it is the centre building
	if settlement == null
		for centreBuilding in SETTLEMENT_MAP
			settlement = SETTLEMENT_MAP.get(centreBuilding)
			if settlement.isMember(this)
				return settlement
		settlement = null
	return settlement

/** Trys to add this unit as settlement. Returns if it could be added. */
public function unit.tryAddSettlement() returns bool
	return this.addSettlement() == Validity.VALID

/** Trys to add this unit given settlement. Returns if it could be added. */
public function unit.tryAddToSettlement(unit centreBuilding) returns bool
	let settlement = SETTLEMENT_MAP.tryGet(centreBuilding)
	if settlement == null
		return false
	if this.getPos().distanceTo(centreBuilding.getPos()) > getBuildingRange(settlement.level) or getRequiredLevel(this.getUnitId()) > settlement.level
		return false
	settlement.addMemeber(this)
	return true

/** Sets system relevant infos for given object in target multiboard. */
public function multiboard.setObjectInfoMultiboard(Settlement s) returns bool
	this.addMultiboardLine("Settlement:", HEADLINE_ICON_PATH)
	this.addMultiboardLine("Level: " + s.level.toString(), LEVEL_ICON_PATH)
	this.addMultiboardLine("Population: " + s.persons.size().toString(), POPULATION_ICON_PATH)
	this.addMultiboardLine("Buildings: " + s.buildings.size().toString(), BUILDINGS_ICON_PATH)
	return true

init
	// Remove settlement from every removed centre building:
	onUnitDeindex() ->
		getIndexingUnit().removeFromSettlement()
	// Register upgrade starts:
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_START) ->
		let building = GetTriggerUnit()
		Validity val
		if isCentreBuilding(building.getUnitId())
			val = building.addSettlement()
		else
			val = building.addToSettlementInRange()
		if val != Validity.VALID
			printTimedToPlayer("Invalid: " + getInvalidMessageText(val), INFO_MSG_TIME, building.getOwner())
			building.issueImmediateOrderById(SpecialOrders.cancel)


// --------------------
// Test code
// --------------------

unit testSettlementSystem_Build_buildingA = null
unit testSettlementSystem_Build_buildingB = null
public function testSettlementSystem_Build(vec2 testCentre)
	let testName = setTestName("testSettlementSystem_Build", true)
	// Remove units blocking the building:
	ForGroup(GetUnitsInRectAll(testCentre.withRadiusRect(128.)), () -> GetEnumUnit().remove())
	// Order to build:
	let unitA = createUnit(Player(0), UnitIdsCustom.AoW.peasant, testCentre, angle(0))
	let storageA = unitA.getStorage()
	storageA.addWare(ware(WareType.WOOD, 4))
	storageA.addWare(ware(WareType.STONE, 3))
	storageA.addWare(ware(WareType.METAL, 2))
	unitA.issuePointOrderById(UnitIdsCustom.AoW.townHall, testCentre)
	unit unitB = null
	Settlement settlementA = null
	testSettlementSystem_Build_buildingA = null
	testSettlementSystem_Build_buildingB = null
	doAfter(MIN_ACTION_SLEEP_TIME) () ->
		// Check settlement mappings:
		let filterA = Filter(() -> GetFilterUnit().getUnitId() == UnitIdsCustom.AoW.townHall)
		testSettlementSystem_Build_buildingA = testCentre.getNearestUnit(128., filterA, Player(0))
		filterA.destr()
		assertNotNull(testName, testSettlementSystem_Build_buildingA)
		settlementA = testSettlementSystem_Build_buildingA.getSettlement()
		assertNotNull(testName, settlementA)
		settlementA.level.assertEquals(testName, 1)
		settlementA.persons.size().assertEquals(testName, 1)
		settlementA.buildings.size().assertEquals(testName, 0)
		assertTrue(testName, settlementA.isMember(unitA))
		// Order to build new structure for settlement:
		unitB = createUnit(Player(0), UnitIdsCustom.AoW.peasant, testCentre.add(384., 0.), angle(0))
		let storageB = unitB.getStorage()
		storageB.addWare(ware(WareType.WOOD, 4))
		unitB.issuePointOrderById(UnitIdsCustom.AoW.peasantHouse, testCentre.add(384., 0.))
	doAfter(2 * MIN_ACTION_SLEEP_TIME) () ->
		// Recheck settlement mappings:
		let filterB = Filter(() -> GetFilterUnit().getUnitId() == UnitIdsCustom.AoW.peasantHouse)
		testSettlementSystem_Build_buildingB = testCentre.add(384., 0.).getNearestUnit(128., filterB, Player(0))
		filterB.destr()
		assertNotNull(testName, testSettlementSystem_Build_buildingB)
		assertNotNull(testName, settlementA)  // DEBUG: settlementA is null in this callback, maybe because it was set in another callback
		settlementA.level.assertEquals(testName, 1)  // FIXME: find solution or split up tests
		settlementA.persons.size().assertEquals(testName, 1)
		settlementA.buildings.size().assertEquals(testName, 1)
		assertTrue(testName, settlementA.isMember(testSettlementSystem_Build_buildingB))
		testFail(testName, "Test implementation not finished yet")  // TODO: finish implementation
	doAfter(3 * MIN_ACTION_SLEEP_TIME) () ->
		// Abort constructions:
		testSettlementSystem_Build_buildingB.issueImmediateOrderById(SpecialOrders.cancel)
		testSettlementSystem_Build_buildingA.issueImmediateOrderById(SpecialOrders.cancel)
		testFinish(testName)
	doAfter(4 * MIN_ACTION_SLEEP_TIME) () ->
		// Remove test units:
		unitA.kill()
		unitA.remove()
		unitB.kill()
		unitB.remove()
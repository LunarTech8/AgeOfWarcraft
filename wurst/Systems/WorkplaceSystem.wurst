/*-------------------------------------------------------------
// Version number:
1.0.0

// Description:
Abstract base class for workplace building systems.

// Changelog:
- 1.0.0:
Initial version.
-------------------------------------------------------------*/
package WorkplaceSystem
import ConstantData
import UtilityFunctions
import StorageSystem
import ErrorHandling


// --------------------
// Data code
// --------------------

/** Returns the worker type of the given workplace type. */
function getWorkerUnitId(int workplaceUnitId) returns int
	switch workplaceUnitId
		case UnitIdsCustom.AoW.lumberMill
			return UnitIdsCustom.AoW.peasant
		case UnitIdsCustom.AoW.blacksmith
			return UnitIdsCustom.AoW.peasant
		case UnitIdsCustom.AoW.lumberCamp
			return UnitIdsCustom.AoW.peasant
		case UnitIdsCustom.AoW.townHall
			return UnitIdsCustom.AoW.peasant
	return 0

/** Returns the maximum allowed amount of workers of the given workplace type. */
function getMaxWorkers(int workplaceUnitId, bool forBuildingSite) returns int
	if forBuildingSite
		switch workplaceUnitId
			case UnitIdsCustom.AoW.townHall
				return 6
			case UnitIdsCustom.AoW.peasantHouse
				return 3
			case UnitIdsCustom.AoW.blacksmith
				return 4
			case UnitIdsCustom.AoW.lumberMill
				return 4
			case UnitIdsCustom.AoW.barracks
				return 5
			case UnitIdsCustom.AoW.lumberCamp
				return 3
	else
		switch workplaceUnitId
			case UnitIdsCustom.AoW.lumberMill
				return 3
			case UnitIdsCustom.AoW.blacksmith
				return 3
			case UnitIdsCustom.AoW.lumberCamp
				return 3
			case UnitIdsCustom.AoW.townHall
				return 3
	return 0


// --------------------
// Functional code
// --------------------

public abstract class Workplace
	protected unit user
	protected group workers

	construct(unit workplace)
		user = workplace
		workers = CreateGroup()

	ondestroy
		workers.destr()

	abstract function transferWares(Storage source, bool takeWares)

	function isValidWorkerType(int unitId) returns bool
		return getWorkerUnitId(user.getUnitId()) == unitId

	function isWorker(unit worker) returns bool
		return workers.contains(worker)

	function getUnit() returns unit
		return user

	function getWorkerCount() returns int
		return workers.size()

	function getFirstWorker() returns unit
		if workers.size() <= 0
			error("Workplace doesn't have any worker")
		return workers.getUnitAt(0)

	function getWorkers() returns group
		let workers = CreateGroup()
		workers.addGroup(this.workers)
		return workers

	function getFreeWorkerSlots() returns int
		return getMaxWorkers(user.getUnitId(), false) - workers.size()

	function getFreeWorkerSlotsForBuildingSite() returns int
		return getMaxWorkers(user.getUnitId(), true) - workers.size()

	function addWorker(unit worker)
		if workers.contains(worker) or workers.size() >= getMaxWorkers(user.getUnitId(), false)
			error("Can't add worker to this workplace")
		workers.addUnit(worker)

	function addWorkerForBuildingSite(unit worker)
		if workers.contains(worker) or workers.size() >= getMaxWorkers(user.getUnitId(), true)
			error("Can't add worker to this workplace")
		workers.addUnit(worker)

	function removeWorker(unit worker)
		if workers.contains(worker) == false
			error("Can't remove worker from this workplace")
		workers.removeUnit(worker)